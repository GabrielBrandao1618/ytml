text                    =  { (!("{" | "}" | "\"") ~ ANY)+ }
int                     = @{ ASCII_DIGIT+ }
string                  = ${ "\"" ~ text ~ "\"" }
tag_name                = @{ (ASCII_ALPHA | "-" | "_")+ }
prop_name               = @{ (ASCII_ALPHA | "-" | "_")+ }
prop_value              =  { string }
tag_props               =  { "(" ~ tag_prop* ~ ")" }
tag_prop                =  { prop_name ~ "=" ~ prop_value }
tag_inner               =  { (tag+ | text)* }
tag_multiplier          =  { "*" ~ int }
tag_shortcut_prop_value =  { (ASCII_ALPHA | ASCII_DIGIT | "-" | "_")+ }
tag_class               =  { "." ~ tag_shortcut_prop_value }
tag_id                  =  { "#" ~ tag_shortcut_prop_value }
tag_modifier            = _{ tag_multiplier | tag_class | tag_id }
tag_modifiers           = ${ tag_modifier* }
tag                     =  { tag_name ~ tag_modifiers ~ tag_props ~ "{" ~ tag_inner ~ "}" }

doc = { SOI ~ tag* ~ EOI }

WHITESPACE = _{ " " | "\n" }
